<basic.rx

{ test if an integer is even, or a rational is an integer }
set is_even (int i)    = bool: i%2=0
set is_integer (rat r) = bool: denom(r)=1

{ test if a ratvec is in fact a vec (integers)}
set is_integer (ratvec v) = bool: denom(v)=1

{ make a rational into an integer if possible }
set rattoint (rat r) = int:
  let (n,d)=%r in if d=1 then n else error("Not an integer") fi

{ make a rational vector into an integer one if possible }
set ratvectovec (ratvec v)= vec:
  let (w,d)=%v in if d=1 then w else error("Not an integer vector") fi

{ define dot product ratvec*ratvec,ratvec*vec,vec*ratvec }
set *(ratvec v,ratvec w)= rat: let (a,b)=%v then (c,d)=%w in a*c/(b*d)

{ transform ParamPol into a list of (Split,Param) pairs; previously built-in }
set %(ParamPol P) = [(Split,Param)]: for c@p in P do (c,p) od

{ cases *@(vec,ratvec) and *@(ratvec,vec) are now handled by coercion }

{ split, simply connected group of given type }
set split_form(RootDatum r)=quasisplit_form(inner_class(r,-id_mat(rank(r))))
set split_form(LieType t)=split_form(simply_connected(t))
set split_group(string s)= RealForm: split_form(s)

{ Quasisplit form: the quasisplit, but not split, form of G, if it exists }
set Quasisplit_form(RootDatum r)=quasisplit_form (inner_class (r,"u"))
set Quasisplit_form(LieType t)= Quasisplit_form(simply_connected(t))

set root_datum(Param p)=RootDatum: real_form(p)

set print_real_forms(RealForm G)= void:
  for n@i in form_names(inner_class(G))
  do prints(i,": ",n)
  od; prints("To define a real form rf do: set rf=real_forms(G)[i]")

set ss_rank(RootDatum rd)= semisimple_rank(rd)

{ test if rartional weight (not necessarily dominant) is regular }
set is_regular(RootDatum rd,ratvec lambda)= bool:
  let regular=true
  in for alpha in positive_coroots(rd) do
       if alpha*lambda=0 then regular:=false fi
     od; regular

set is_regular(RealForm G,ratvec lambda)=is_regular(root_datum(G),lambda)
set is_regular(Param p)=is_regular(real_form(p),infinitesimal_character(p))

set rhocheck(RootDatum rd)= ratvec: rho(dual(rd))
set rhocheck(RealForm G)  = ratvec: rhocheck(root_datum(G))

{ make rho as an vector of integers if possible }
set intrho (RealForm G) = vec: ratvectovec(rho(G))
set intrho (RootDatum r)= vec: ratvectovec(rho(r))

{ some data defined by a Param p }
set integrality_datum(Param p)= RootDatum:
  integrality_datum(real_form(p),infinitesimal_character(p))

{ extract x,lambda-rho,lambda,nu from p
  lambda-rho =vec,  lambda= ratvec }
set x(Param p) = KGBElt: let (x,,) =%p in x
set lambdaminusrho(Param p) = vec: let (,lambda,) =%p in lambda
set lambda(Param p) = ratvec: lambdaminusrho(p)+rho(real_form(p))
set nu(Param p) = ratvec: let (,,nu) =%p in nu

{ alternate syntax for param command:
  param(G,3,...) instead of param(KGB(G,3),...)  }
set param(RealForm G,int k, vec lambda,ratvec nu)=param(KGB(G,k),lambda,nu)

{ normparam(G,x,lambda,nu)=param(G,x,lambda-rho(G),nu), so you can enter
  lambda without the rho shift (don't need a denominator if lambda\in X^*(H)) }
set normparam (RealForm G,int x,ratvec lambda,ratvec nu) =
  param(G,x,ratvectovec(lambda-rho(G)),nu)

{ finite dimensional with highest weight tau in X^*
  I'm not sure this always works; it returns
    param(last KGB element,tau,tau+rho) so lambda=tau+rho }
set finite_dimensional(RealForm G,vec tau) = Param:
  param(KGB(G,KGB_size(G)-1),tau,tau+rho(G))

{ get just the parameters from an n_block,
  just a shorcut to: (params,)=n_block(p) }
set get_n_block (Param p) = [Param]: let (params,)=n_block(p) in params

{ trivial representation of G, see finite_dimensional }
set trivial(RealForm G) = Param: finite_dimensional(G,null(rank(G)))

{ returns [Param] list of parameters in block of trivial }
set trivial_block(RealForm G) = [Param]: get_n_block(trivial(G))

{ spherical principal series of split group
  assumes G is split, otherwise no guarantees
  specify nu as a vec or ratvec }
set spherical_principal_series (RealForm G, ratvec nu)= Param:
  let lambda=null(rank(G)) in param(KGB(G,KGB_size(G)-1),lambda,nu)

{ replace non-final parameter with [Param p] of final ones
  uses this feature of ParamPol, but ignores any coefficients }
set make_final (Param p)= [Param]: for c@q in ParamPol: p do q od

{ nicer display of Splits }
set prettyPrint (Split w) = string:
  let (a,b)=%w in
  if   b=0 then int_format(a)
  elif a=0 then
       if b=1 then "s" elif b=-1 then "-s" else int_format(b)#"s" fi
  elif b=1 then int_format(a)#"+s"
  elif b=-1 then int_format(a)#"-s"
  elif b<-1 then int_format(a)#int_format(b)#"s"
  else int_format(a)#"+"#int_format(b)#"s"
  fi

{ nice output of ParamPol:
  prettyPrint the coefficients, print terms in order of x }
set prettyPrint (ParamPol P)= void:
for w@p in P
do prints("(",prettyPrint(w),")*",p,", ",infinitesimal_character(p)) od

{ evaluate at s=t for t integer (or rational?): replace w=a+bs with a+bt }
{ would seem to make sense only if t^2=1, so if either t=1 or t=-1 }
set  #(Split w,int t)=let (a,b)=%w in int: a+t*b
set  #(Split w,rat t)=let (a,b)=%w in rat: a+t*b
set  #(ParamPol P,int t)= ParamPol: { with integer coefficiens }
  null_module(real_form(P)) +  for w@q in P do (w#t,q) od

{ evaluate polynomial at split }
set eval(vec v,Split w)= Split:
  let sum=Split:0 in  for i:#v downto 0 do sum:= v[i] + w*sum od; sum

set at_s(vec v)=eval(v,s)

{ evaluate polynomial at integer }
set eval(vec v,int k)= int:
  let sum=0 in  for i:#v downto 0 do sum:= v[i] + k*sum od; sum
